###
 0000000  000   000  00000000  00000000  000000000
000       000   000  000       000          000   
0000000   000000000  0000000   0000000      000   
     000  000   000  000       000          000   
0000000   000   000  00000000  00000000     000   
###

use ../kxk ▪ $ elem kstr post
use ◆ color

class Sheet

    @: ->
        
        @view   =$ "#sheet"
        @calc   = elem class:'sheet-calc'
        @result = elem class:'sheet-result'
        @view.appendChild @calc
        @view.appendChild @result
        post.on 'sheet'   @onSheet
        post.on 'resize'  @onResize
    
    onResize: =>
        
        if window.innerHeight <= window.WIN_MIN_HEIGHT
            @view.style.display = 'none'
        else
            @view.style.display = 'flex'
        
    compact: ○->
        
        info =○ kakao 'window.frameInfo'
        kakao 'window.setFrame' x:info.frame.x, y:info.frame.y, w:476 h:window.WIN_MIN_HEIGHT
        @
        
    expand: ○->
        
        info =○ kakao 'window.frameInfo'

        spaceAbove = (info.screen.h + info.screen.y) - (info.frame.h + info.frame.y)

        if spaceAbove > 34
            
            add = @view.scrollHeight-@view.getBoundingClientRect().height+4
            kakao 'window.setFrame' x:info.frame.x, y:info.frame.y, w:476 h:max 656, info.frame.h+add
        @
        
    clear: =>
        
        @calc.innerHTML   = ''
        @result.innerHTML = ''
        @
            
    onSheet: action =>
        
        if
            action == 'clear'    ➜ ⮐ @clear()
            action == 'collapse' ➜ ⮐ @clear().compact()

            action.text != kstr action.val
                
                @calc.appendChild   elem class:'sheet-line calc'   html:color action.text #+ ' ='
                @result.appendChild elem class:'sheet-line result' html:color action.val
            ➜
                @calc.appendChild   elem class:'sheet-line calc'   html:''
                @result.appendChild elem class:'sheet-line result' html:color action.val
            
        @result.lastChild.scrollIntoView()
        @expand()
        
export Sheet
