###
     0000000  00000000    0000000   000   000  000   000
    000       000   000  000   000  000 0 000  0000  000
    0000000   00000000   000000000  000000000  000 0 000
         000  000        000   000  000   000  000  0000
    0000000   000        000   000  00     00  000   000
    
    clones subprojects into their own application folder
###

use ../kxk ◆ pretty nfs slash 

bundle = path: slash.path ◆dir, '../..'

export (args) ○->
    
    app = args[0]
    
    ⮐ error 'no app to spawn?' if empty app
    
    prt  = args[1]
    prt ?= slash.untilde "~/s/#{app}"
    
    tgt  = slash.path prt, app + '.app'
    
    log b5('ꦌ'), y4(app), b3(' '), pretty.filePath(bundle.path, w8, b3), b3('▸'), pretty.filePath(tgt, y5, y1)
    
    prtExists = ○ nfs.dirExists prt
    
    ⮐ error "parent directory #{tgt} doesn't exist" if not prtExists
    
    tmp = slash.tmpfile()
    ○ nfs.move slash.path(tgt, '.stash'), tmp # move stash out of the way
    
    if ○ nfs.exists tgt ➜ ○ nfs.remove tgt # nuke target directory
        
    cpy = s d ○-> ○ nfs.copy slash.path(bundle.path, s), slash.path(tgt, d)
        
    ○ nfs.copy bundle.path, tgt # copy the whole bundle over
    
    ○ cpy "pyg/#{app}.pug"                      'pyg/index.pug'
    ○ cpy "js/#{app}.html"                      'js/index.html'
    ○ cpy "Contents/Resources/img/#{app}.icns"  'Contents/Resources/img/app.icns'
    ○ cpy "Contents/#{app}.plist"               'Contents/Info.plist'
    
    if app != 'ko'
        
        remove = [
            'js/ko'
            'kode/ko'
            'pyg/ko'
            'pyg/ko.pug'
            'js/css/ko'
            
            'Contents/Resources/img/ko.icns'
            'Contents/Resources/img/ko.png'
            'Contents/Resources/img/ko.pxm'
            'Contents/Resources/img/menu_ko.png'
            'Contents/Resources/menu_ko.noon'
                        
            'Contents/Resources/font/Lilex-Bold.woff2'
            'Contents/Resources/font/devopicons.woff2'
            'Contents/Resources/font/fa-regular-400.woff2'
            'Contents/Resources/font/fa-solid-900.woff2'
            'Contents/Resources/font/file-icons.woff2'
            'Contents/Resources/font/fontawesome.woff2'
            'Contents/Resources/font/mfixx.woff2'
            'Contents/Resources/font/octicons.woff2'
            ]
    
        for f in remove
            ○ nfs.remove slash.path tgt, f
    
    remove = [ 
        '.stash'
        'Contents/Resources/img/zen.png'
        ]

    for f in remove
        ○ nfs.remove slash.path tgt, f
        
    ○ nfs.move tmp, slash.path(tgt, '.stash') # put stash back in place    
            
            