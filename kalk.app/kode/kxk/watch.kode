###
000   000   0000000   000000000   0000000  000   000
000 0 000  000   000     000     000       000   000
000000000  000000000     000     000       000000000
000   000  000   000     000     000       000   000
00     00  000   000     000      0000000  000   000
###

# use events # this is run in node

use ◆ slash fs events

function Watch extends events

    @: (path, opt) ->
        
        super()
        
        @dir  = slash.path path
        @opt  = opt ? {}
        @last = {}
        
        fs.exists @dir, (stat) => if stat then @watchDir()
       
    @dir: (path, opt) -> new Watch path, opt
    
    @watch: (path, opt) ->
    
        if opt.cb
            fs.isDir path, (stat) ->
                if stat
                    opt.cb Watch.dir path, opt
                else
                    opt.cb Watch.file path, opt
        else
            if fs.isDir path
                Watch.dir path, opt
            else
                Watch.file path, opt
    
    @file: (path, opt) ->
            
        w = Watch.dir slash.dir(path), opt
        w.file = slash.path path
        w
        
    # 0000000    000  00000000   
    # 000   000  000  000   000  
    # 000   000  000  0000000    
    # 000   000  000  000   000  
    # 0000000    000  000   000  
    
    watchDir: ○->
        
        return if not @dir
        
        @watch = fs.watch @dir
        @watch.on 'error' (err) => error "watch dir:'#{@dir}' error: #{err}"
        @watch.on 'change' @onChange
        
        if @opt.recursive
            
            items =○ fs.list @dir
            
            @watchers = []
            
            for item in items
                if item.type == 'dir'
                    if item.file in ['node_modules' '.git']
                        log b5 item.file
                        continue
                    # log g3(item.file), y5 item.path
                    
                    watch = fs.watch item.path
                    @watchers.push watch
                    
                    change = (dir) => (chg, pth) => @onChange chg, pth, dir
                    watch.on 'error' (err) -> error "watch subdir:'#{item.path}' error: #{err}"
                    watch.on 'change' change item.path
                    
    # 000   0000000   000   000   0000000   00000000   00000000  
    # 000  000        0000  000  000   000  000   000  000       
    # 000  000  0000  000 0 000  000   000  0000000    0000000   
    # 000  000   000  000  0000  000   000  000   000  000       
    # 000   0000000   000   000   0000000   000   000  00000000  
    
    ignore: (path) ->
        
        if @opt.ignore
            for regex in @opt.ignore
                if new RegExp(regex).test path
                    return true
       
    #  0000000  000       0000000    0000000  00000000  
    # 000       000      000   000  000       000       
    # 000       000      000   000  0000000   0000000   
    # 000       000      000   000       000  000       
    #  0000000  0000000   0000000   0000000   00000000  
    
    close: ->
        
        @watch?.close()
        delete @watch
        delete @dir
        if @opt.recursive
            for watch in @watchers 
                watch.close()
            delete @watchers
                    
    #  0000000   000   000   0000000  000   000   0000000   000   000   0000000   00000000  
    # 000   000  0000  000  000       000   000  000   000  0000  000  000        000       
    # 000   000  000 0 000  000       000000000  000000000  000 0 000  000  0000  0000000   
    # 000   000  000  0000  000       000   000  000   000  000  0000  000   000  000       
    #  0000000   000   000   0000000  000   000  000   000  000   000   0000000   00000000  
    
    onChange: (change, path, dir=@dir) =>
        
        return if @ignore path
        
        path = slash.path dir, path
        
        if @file and @file != path
            return
        
        if fs.isDir path
            if @file
                log 'ignore dir' path
                return
                        
        if stat = fs.exists path
        
            if @opt.skipSave and path == @remove?.path # and change == 'rename'
                clearTimeout @remove.timer
                clearRemove = => delete @remove
                setTimeout clearRemove, 100
                return
            
            if path == @last?.path and stat.mtime.getTime() == @last?.mtime?.getTime()
                return # unchanged
            
            @last = mtime:stat.mtime, path:path
            @emit 'change' dir:dir, path:path, change:change, watch:@
            
        else
            
            if @opt.skipSave
                @remove =
                    path:  path
                    timer: setTimeout ((d,p,w)->->
                        delete w.remove 
                        w.emit 'change' dir:d, path:p, change:'remove', watch:w)(dir,path,@), 100
            else if @opt.emitRemove
                @emit 'change' dir:dir, path:path, change:'remove', watch:@
        
export Watch
